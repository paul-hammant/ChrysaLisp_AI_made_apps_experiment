(include "lib/asm/func.inc")
(include "./class.inc")
(include "././hset/class.inc")
(include "././list/class.inc")
(include "././stream/class.inc")
(include "sys/str/class.inc")
(include "sys/statics/class.inc")

(gen-vtable 'sym)
(gen-type 'sym)

(def-method 'sym :statics_init)
	;trashes
	;:r0-:r14

	(def-vars
		(ptr sym syms)
		(pubyte next)
		(uint length))

	(push-scope)
	(entry 'sym :statics_init {})

	;init symbol set
	(call 'hset :create `((@ ,(f-path 'sym :same)) num_sym_buckets) '(:r0))
	(fn-bind 'sys/statics/statics :r1)
	(assign '(:r0) '((:r1 statics_sym_intern)))

	;add static symbols
	(call 'list :create :nil {syms})
	(call 'list :set_cap (cat {syms, } (str (length *static_syms*))))
	(assign {$props} {next})
	(loop-start)
		(call 'sym :intern_cstr {next} {sym})
		(call 'list :push_back {syms, sym})
		(call 'sys_str :length {next} {_, length})
	(loop-until {(next + length + +byte_size => next) = $props_end})
	(assign {syms} {@sys/statics/statics.statics_sym_syms})

	(exit 'sym :get_static_sym {sym})
	(pop-scope)
	(return)

;;;;;;;;;;;;;;;;;;;;;;;;;
; interned static symbols
;;;;;;;;;;;;;;;;;;;;;;;;;

(vp-label 'props)
	(each (lambda (_) (vp-cstr _)) *static_syms*)
(vp-label 'props_end)

(def-func-end)

(def-method 'sym :get_static_sym)
	;inputs
	;:r1 = static sym num (uint)
	;outputs
	;:r1 = sym object (ptr)
	;trashes
	;:r1, :r3

	(entry 'sym :get_static_sym '(:r1))

	(fn-bind 'sys/statics/statics :r3)
	(assign '((:r3 statics_sym_syms)) '(:r3))
	(class/array/get_elem :r3 :r1 :r1 :r3)

	(exit 'sym :get_static_sym '(:r1))
	(vp-ret)

(def-func-end)

(def-method 'sym :ref_static_sym)
	;inputs
	;:r1 = static sym num (uint)
	;outputs
	;:r1 = sym object (ptr)
	;trashes
	;:r1, :r3

	(entry 'sym :ref_static_sym '(:r1))

	(call 'sym :get_static_sym '(:r1))
	(class/obj/ref :r1 :r3)

	(exit 'sym :ref_static_sym '(:r1))
	(vp-ret)

(def-func-end)

(def-method 'sym :intern_str)
	;inputs
	;:r0 = str object (ptr)
	;outputs
	;:r0 = interned sym object (ptr)
	;trashes
	;:r0-:r14
	;info
	;input str IS NOT derefed

	(entry 'sym :intern_str '(:r0))

	(vp-push :r0)
	(fn-bind 'sys/statics/statics :r2)
	(call 'hset :find '((:r2 statics_sym_intern) :r0) '(_ :r1 :r2))
	(vp-pop :r0)
	(vpif '(:r1 = 0))
		(vp-cpy-rr :r2 :r7)
		(call 'sym :create_from_buffer '((& :r0 str_data) (:r0 str_length)) '(:r0))
		(assign `((@ ,(f-path 'sym :vtable)) :r0 :r7) '(:r2 :r1 :r0))
		(assign '(:r2) '((:r1 obj_vtable)))
		(call 'list :push_back '(:r0 :r1) '(_ :r1 _ _))
	(else)
		(assign '((:r1 0)) '(:r1))
	(endif)
	(class/obj/ref :r1 :r0)

	(exit 'sym :intern_str '(:r1))
	(vp-ret)

(def-func-end)

(def-method 'sym :intern_cstr)
	;inputs
	;:r0 = c string pointer (pubyte)
	;outputs
	;:r0 = interned sym object (ptr)
	;trashes
	;:r0-:r11

	(entry 'sym :intern_cstr '(:r0))
	(call 'sys_str :length '(:r0) '(:r0 :r1))
	(jump 'sym :intern_pstr '(:r0 (& :r0 :r1)))

(def-func-end)

(def-method 'sym :intern_pstr)
	;inputs
	;:r0 = start (pubyte)
	;:r1 = end (pubyte)
	;outputs
	;:r0 = interned sym object (ptr)
	;trashes
	;:r0-:r11

	(def-vars
		(ptr intern)
		(pubyte str_begin str_end)
		(uint hcode))

	(vp-def (iter_begin iter_end str_begin str_end str_size bucket symbol str_len)
		'(:r6 :r7 :r8 :r9 :r10 :r11))

	(push-scope)
	(entry 'sym :intern_pstr {str_begin, str_end})

	(call 'str :hash_pstr {str_begin, str_end} {hcode})
	(assign {@sys/statics/statics.statics_sym_intern} {intern})
	(call 'list :get_elem {intern->hset_buckets, hcode % intern->hset_num_buckets} `(_ ,bucket))

	(assign {str_end, str_begin} `(,str_end ,str_begin))
	(class/array/get_both bucket iter_begin iter_end)
	(vp-cpy-rr str_end str_size)
	(vp-sub-rr str_begin str_size)
	(vpif `(,iter_begin /= ,iter_end))
		(loop-start)
			(assign `((,iter_begin 0)) `(,symbol))
			(vp-add-cr +ptr_size iter_begin)
			(assign `((,symbol str_length)) `(,str_len))
			(vpif `(,str_len = ,str_size))
				(call 'str :same_pstr `(,str_begin ,str_end (& ,symbol str_data)) '(found))
				(gotoif `(,found = 0) 'exit)
			(endif)
		(loop-until `(,iter_begin = ,iter_end))
	(endif)

	(call 'sym :create_from_buffer `(,str_begin ,str_size) `(,symbol))
	(assign `((@ ,(f-path 'sym :vtable))) `(,str_len))
	(assign {hcode} `(,str_size))
	(assign `(,str_len ,str_size) `((,symbol obj_vtable) (,symbol str_hashcode)))
	(call 'list :push_back `(,bucket ,symbol) `(_ ,symbol _ _))

(vp-label 'exit)
	(class/obj/ref symbol str_len)
	(exit 'sym :intern_pstr `(,symbol))
	(pop-scope)
	(return)

(def-func-end)

(def-method 'sym :print)
	;inputs
	;:r0 = sym object (ptr)
	;:r1 = stream object (ptr)
	;outputs
	;:r0 = sym object (ptr)
	;trashes
	;:r1-:r14

	(entry 'sym :print '(:r0 :r1))

	(vp-push :r0)
	(vp-cpy-rr :r1 :r3)
	(call 'stream :write '(:r3 (& :r0 str_data) (:r0 str_length)))
	(vp-pop :r0)

	(exit 'sym :print '(:r0))
	(vp-ret)

(def-func-end)
